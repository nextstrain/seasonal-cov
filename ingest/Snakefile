# Use default configuration values. Override with Snakemake's --configfile/--config options.
configfile: "defaults/config.yaml"


rule all:
    input:
        expand("results/{virus}/sequences.fasta", virus=config["viruses"]),
        expand("results/{virus}/metadata.tsv", virus=config["viruses"]),


include: "rules/fetch_from_ncbi.smk"
include: "rules/curate.smk"


# If included, the nextclade rules will create the final metadata TSV
# by joining the Nextclade output with the metadata. However, if not
# including nextclade, we have to rename the subset metadata TSV to
# the final metadata TSV.
if "nextclade" in config:

    include: "rules/nextclade.smk"

else:

    rule create_final_metadata:
        input:
            metadata="results/{virus}/subset_metadata.tsv",
        output:
            metadata="results/{virus}/metadata.tsv",
        shell:
            r"""
            mv {input.metadata:q} {output.metadata:q}
            """

rule clean:
    params:
        targets = [
            ".snakemake",
            "benchmarks",
            "data",
            "logs",
            "results",
            ]
    shell:
        r"""
        rm -rfv {params.targets}
        """


# Import custom rules provided via the config.
if "custom_rules" in config:
    for rule_file in config["custom_rules"]:

        include: rule_file
